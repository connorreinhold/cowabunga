/*
   Simple +/-/* expression language; 
   parser evaluates constant expressions on the fly
*/

package cyr7.parser;

import java_cup.runtime.*;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import cyr7.ast.*;
import cyr7.ast.expr.*;
import cyr7.ast.expr.binexpr.*;
import cyr7.ast.expr.literalexpr.*;
import cyr7.ast.expr.unaryexpr.*;
import cyr7.ast.stmt.*;
import cyr7.ast.type.*;
import cyr7.exceptions.*;
import cyr7.lexer.LexerUtil;
import java.util.List;
import java.util.LinkedList;
import java.util.Collections;
import java.util.Optional;

parser code {:

@Override
public void unrecovered_syntax_error(Symbol cur_token) throws Exception {
    if (cur_token instanceof ComplexSymbol) {
        ComplexSymbol sym = (ComplexSymbol) cur_token;
        throw new UnexpectedTokenException(sym);
    } else {
        // We don't know the line or column
        throw new UnexpectedTokenException(
                    cur_token.sym, cur_token.value, 1, 1);
    }
}

@Override
public void syntax_error(Symbol cur_token) {
    // Do nothing
}

:};

action code {:
:}

/* define how to connect to the scanner! */
init with {:
:};

scan with {: 
    Symbol sym = getScanner().next_token();
    return (sym != null) ? 
            sym 
            : getSymbolFactory().newSymbol("END_OF_FILE",EOF_sym());
:};

/* Terminals (tokens returned by the scanner). */


terminal IXI_FILE, XI_FILE;
// Punctuation
terminal IF, ELSE, RETURN, USE, WHILE, LENGTH;

// Identifier
terminal String ID;

// Types
terminal String TYPE_INT, TYPE_BOOL;

// Values
terminal String INT_LITERAL;
terminal String STRING_LITERAL;
terminal String CHAR_LITERAL;
terminal Boolean BOOL_LITERAL;

// Punctuation
terminal L_PAREN, R_PAREN, L_SQ_BRKT, R_SQ_BRKT, L_BRACE, R_BRACE;
terminal COLON, SEMICOLON, COMMA, UNDERSCORE;

// Operators
terminal ASSIGN,
         PLUS,
         MINUS,
         MULT,
         HIGH_MULT,
         DIVIDE,
         EQUALS,
         NOT_EQUALS,
         LT,
         LTE,
         GT,
         GTE,
         NEG_BOOL,
         NEG_INT,
         REMAINDER,
         LOGICAL_AND,
         LOGICAL_OR
         ;

terminal FUNCTION_CALL,
         ARRAY_ACCESS;

// Program-wide
nonterminal IProgramNode Program;

// Type Expressions

nonterminal ITypeExprNode TypeExpr;
nonterminal PrimitiveTypeNode Primitive;
nonterminal LinkedList<Optional<ExprNode>> TypeExprArrayList;

// Ixi program

nonterminal IxiProgramNode IxiProgram;
nonterminal LinkedList<FunctionHeaderDeclNode> FunctionHeaderDeclList;
nonterminal FunctionHeaderDeclNode FunctionHeaderDecl;

nonterminal LinkedList<VarDeclNode> FunctionVarDeclList;

nonterminal LinkedList<ITypeExprNode> ReturnType;
nonterminal LinkedList<ITypeExprNode> ReturnTypeList;

// Xi program

nonterminal XiProgramNode XiProgram;
nonterminal LinkedList<UseNode> UseList;
nonterminal LinkedList<FunctionDeclNode> FunctionDeclList;
nonterminal FunctionDeclNode FunctionDecl;

nonterminal StmtNode XiStatement;
nonterminal LinkedList<StmtNode> XiStatementList;
nonterminal StmtNode SingleXiStatement;
nonterminal ReturnStmtNode ReturnStatement;
nonterminal BlockStmtNode BlockStatement;
nonterminal ProcedureStmtNode ProcedureStatement;
nonterminal IfElseStmtNode IfElseStatement;
nonterminal WhileStmtNode WhileStatement;
nonterminal AssignmentStmtNode AssignmentStatement;
nonterminal AssignAccessNode AssignAccess;

nonterminal VarDeclStmtNode VarDeclStatement;
nonterminal VarDeclNode VarDecl;

nonterminal LinkedList<ExprNode> ExprList;

nonterminal ExprNode Expr;
nonterminal FunctionCallExprNode FunctionCall;
nonterminal LinkedList<ExprNode> ArgParamList;

nonterminal ExprAccessNode Access;
nonterminal ExprNode LiteralExpr;
nonterminal ExprNode UnaryExpr;
nonterminal ExprNode IntegerExpr;
nonterminal ExprNode BoolExpr;
nonterminal ExprNode ArrayExpr;
nonterminal LinkedList<ExprNode> ArrayElements;

nonterminal VarDeclNode VarDeclArrayInit;
nonterminal LinkedList<Optional<ExprNode>> VarDeclDimensionedBracket;
nonterminal LinkedList<Optional<ExprNode>> VarDeclDimensionlessBracket;

nonterminal LinkedList<Optional<VarDeclNode>> MultiVarDeclList;

// Precedence

precedence nonassoc SEMICOLON;
precedence nonassoc L_BRACE;

precedence nonassoc IF;
precedence nonassoc ELSE;

precedence nonassoc ID;
precedence left LOGICAL_OR;
precedence left LOGICAL_AND;
precedence left EQUALS, NOT_EQUALS;
precedence left LT, LTE, GTE, GT;
precedence left PLUS, MINUS;
precedence left MULT, HIGH_MULT, DIVIDE, REMAINDER;
precedence nonassoc NEG_INT, NEG_BOOL;
precedence left FUNCTION_CALL, ARRAY_ACCESS;
precedence left LENGTH;

// Grammar

start with Program;

Program ::=
    IXI_FILE IxiProgram:ixi {: RESULT = ixi; :}
    | XI_FILE XiProgram:xi {: RESULT = xi; :}
    ;

// Ixi Grammar

IxiProgram ::=
    FunctionHeaderDeclList:lst {: 
        RESULT = new IxiProgramNode(lstxleft, lst); 
    :};

FunctionHeaderDeclList ::=
    FunctionHeaderDeclList:lst FunctionHeaderDecl:decl {:
            lst.addLast(decl);
            RESULT = lst;
    :}
    | FunctionHeaderDeclList:lst SEMICOLON {: RESULT = lst; :}
    | {: RESULT = new LinkedList<>(); :}
    ;

FunctionHeaderDecl ::=
    ID:id L_PAREN FunctionVarDeclList:l R_PAREN ReturnType:returnList {:
            RESULT = new FunctionHeaderDeclNode(idxleft, id, l, returnList);
    :}
    ;

FunctionVarDeclList ::=
    VarDecl:v COMMA FunctionVarDeclList:l {:
            l.addFirst(v);
            RESULT = l;
    :}
    | VarDecl:v {: RESULT = new LinkedList<>(List.of(v)); :}
    | {: RESULT = new LinkedList<>(); :}
    ;

ReturnType ::=
    COLON TypeExpr:t ReturnTypeList:lst {: 
        lst.addFirst(t); 
        RESULT = lst; 
    :}
    | {: RESULT = new LinkedList<>(); :}
    ;

ReturnTypeList ::=
    ReturnTypeList:lst COMMA TypeExpr:t {: 
        lst.addLast(t); 
        RESULT = lst; 
    :}
    | {: RESULT = new LinkedList<>(); :}
    ;

// Xi Grammar

XiProgram ::=
    UseList:uses FunctionDeclList:functions 
    {: RESULT = new XiProgramNode(usesxleft, uses, functions); :}
    ;

UseList ::=
    USE:u ID:id UseList:l {: 
        l.addFirst(new UseNode(uxleft, id)); 
        RESULT = l; 
    :}
    | {: RESULT = new LinkedList<>(); :}
    ;

// Xi statements with no semicolon excluding return
XiStatement ::=
    BlockStatement:s {: RESULT = s; :}
    | ProcedureStatement:s {: RESULT = s; :}
    | IfElseStatement:s {: RESULT = s; :}
    | WhileStatement:s {: RESULT = s; :}
    | AssignmentStatement:s {: RESULT = s; :}
    | VarDeclStatement:s {: RESULT = s; :}
    ;

// Xi statement list excluding return
XiStatementList ::=
    XiStatement:s XiStatementList:l {: l.addFirst(s); RESULT = l; :}
    | SEMICOLON XiStatementList:l {: RESULT = l; :}
    | {: RESULT = new LinkedList<>(); :}
    ;

// Xi statement with or without a semicolon including return
SingleXiStatement ::=
    XiStatement:s {: RESULT = s; :}
    | XiStatement:s SEMICOLON {: RESULT = s; :}
    | ReturnStatement:s {: RESULT = s; :}
    | ReturnStatement:s SEMICOLON {: RESULT = s; :}
    ;

FunctionDeclList ::=
    FunctionDecl:fd FunctionDeclList:l {: l.addFirst(fd); RESULT = l; :}
    | {: RESULT = new LinkedList<>(); :}
    ;

FunctionDecl ::=
    FunctionHeaderDecl:header BlockStatement:block {:
            RESULT = new FunctionDeclNode(headerxleft, header, block);
        :}
    ;

BlockStatement ::=
    L_BRACE:b XiStatementList:l R_BRACE {: 
        RESULT = new BlockStmtNode(bxleft, l); 
    :}
    | L_BRACE:b XiStatementList:l ReturnStatement:r R_BRACE {: 
        l.addLast(r); RESULT = new BlockStmtNode(bxleft, l); 
    :}
    | L_BRACE:b XiStatementList:l ReturnStatement:r SEMICOLON R_BRACE {: 
        l.addLast(r); RESULT = new BlockStmtNode(bxleft, l); 
    :}
    ;

ReturnStatement ::=
    RETURN:r ExprList:l {: 
        RESULT = new ReturnStmtNode(rxleft, l); 
    :}
    ;

ProcedureStatement ::=
    FunctionCall:c {: 
        RESULT = new ProcedureStmtNode(cxleft, c); 
    :}
    ;

IfElseStatement ::=
    IF:i Expr:e SingleXiStatement:s {: 
        RESULT = new IfElseStmtNode(ixleft, e, s, Optional.empty()); 
    :}
    | IF:i Expr:e SingleXiStatement:s ELSE SingleXiStatement:b {:
            RESULT = new IfElseStmtNode(ixleft, e, s, Optional.of(b));
    :}
    ;

WhileStatement ::=
    WHILE:w Expr:e XiStatement:s {: 
        RESULT = new WhileStmtNode(wxleft, e, s); 
    :}
    ;

AssignmentStatement ::=
    AssignAccess:a ASSIGN Expr:e {: 
        RESULT = new AssignmentStmtNode(axleft, a, e); 
    :}
    ;

AssignAccess ::=
    AssignAccess:a L_SQ_BRKT Expr: e R_SQ_BRKT {: 
        RESULT = new ArrayAssignAccessNode(axleft, a, e); 
    :}
    | ID:id {: 
        RESULT = new VariableAssignAccessNode(idxleft, id); 
    :}
    ;

VarDeclStatement ::=
    VarDeclArrayInit:arr {:
            LinkedList<Optional<VarDeclNode>> l = new LinkedList<>();
            l.addFirst(Optional.of(arr));
            RESULT = new VarDeclStmtNode(arrxleft, l, Optional.empty());
    :}
    | UNDERSCORE:u ASSIGN FunctionCall:e {:
            LinkedList<Optional<VarDeclNode>> l = new LinkedList<>();
            l.addFirst(Optional.empty());
            RESULT = new VarDeclStmtNode(uxleft, l, Optional.of(e));
    :}
    | VarDecl:v ASSIGN Expr:e {:
            LinkedList<Optional<VarDeclNode>> l = new LinkedList<>();
            l.addFirst(Optional.of(v));
            RESULT = new VarDeclStmtNode(vxleft, l, Optional.of(e));
    :}
    | MultiVarDeclList:l {:
            RESULT = new VarDeclStmtNode(lxleft, l, Optional.empty());
    :}
    | VarDecl:v COMMA MultiVarDeclList:l ASSIGN FunctionCall:e {:
            l.addFirst(Optional.of(v));
            RESULT = new VarDeclStmtNode(vxleft, l, Optional.of(e));
    :}
    | UNDERSCORE:u COMMA MultiVarDeclList:l ASSIGN FunctionCall:e {:
            l.addFirst(Optional.empty());
            RESULT = new VarDeclStmtNode(uxleft, l, Optional.of(e));
    :}
    ;

MultiVarDeclList ::=
    VarDecl:v COMMA MultiVarDeclList:l {:
            l.addFirst(Optional.of(v));
            RESULT = l;
    :}
    | UNDERSCORE COMMA MultiVarDeclList:l {:
            l.addFirst(Optional.empty());
            RESULT = l;
    :}
    | VarDecl:v  {:
            LinkedList<Optional<VarDeclNode>> l = new LinkedList<>();
            l.addFirst(Optional.of(v));
            RESULT = l;
    :}
    | UNDERSCORE {:
            LinkedList<Optional<VarDeclNode>> l = new LinkedList<>();
            l.addFirst(Optional.empty());
            RESULT = l;
    :}
    ;

VarDeclArrayInit ::=
    ID:id COLON Primitive:p L_SQ_BRKT Expr:v R_SQ_BRKT 
        VarDeclDimensionedBracket:l {:
            l.addFirst(Optional.of(v));
            ITypeExprNode t = ITypeExprNode.fromDimensionList(p, l);
            RESULT = new VarDeclNode(idxleft, id, t);
    :}
    ;

VarDeclDimensionedBracket ::=
    L_SQ_BRKT Expr:v R_SQ_BRKT VarDeclDimensionedBracket:l {:
            l.addFirst(Optional.of(v));
            RESULT = l;
    :}
    | VarDeclDimensionlessBracket:l {: 
            RESULT = l; 
    :}
    ;

VarDeclDimensionlessBracket ::= 
    L_SQ_BRKT R_SQ_BRKT VarDeclDimensionlessBracket:l {:
            l.addFirst(Optional.empty());
            RESULT = l;
    :}
    | {: RESULT = new LinkedList<>(); :}
    ;

VarDecl ::=
    ID:id COLON TypeExpr:te {: 
        RESULT = new VarDeclNode(idxleft, id, te); 
    :}
    ;

Primitive ::=
    TYPE_INT:i {: 
        RESULT = new PrimitiveTypeNode(ixleft, PrimitiveEnum.INT); 
    :}
    | TYPE_BOOL:b {: 
        RESULT = new PrimitiveTypeNode(bxleft, PrimitiveEnum.BOOL); 
    :}
    ;

TypeExpr ::=
    Primitive:p L_SQ_BRKT R_SQ_BRKT TypeExprArrayList:l {:
            l.addFirst(Optional.empty());
            RESULT = ITypeExprNode.fromDimensionList(p, l);
    :}
    | Primitive:p {: RESULT = p; :}
    ;

TypeExprArrayList ::=
    L_SQ_BRKT R_SQ_BRKT TypeExprArrayList:l {: 
        l.addFirst(Optional.empty()); RESULT = l; 
    :}
    | {: RESULT = new LinkedList<>(); :} ;

ExprList ::=
    Expr:e COMMA ExprList:l {: l.addFirst(e); RESULT = l; :}
    | Expr:e {: RESULT = new LinkedList<>(List.of(e)); :}
    | {: RESULT = new LinkedList<>(); :}
    ;

Expr ::=
    FunctionCall:e {: RESULT = e; :}
    | Access:e {: RESULT = e; :}
    | LiteralExpr:e  {: RESULT = e; :}
    | UnaryExpr:e {: RESULT = e; :}
    | IntegerExpr:e {: RESULT = e; :}
    | BoolExpr:e {: RESULT = e; :}
    | L_PAREN Expr:e R_PAREN {: RESULT = e; :}
    | ArrayExpr:e {: RESULT = e; :}
    ;

// For an explanation of %prec at
// https://www.cs.princeton.edu/~appel/modern/java/CUP/manual.html#cpp
FunctionCall ::=
    ID:id L_PAREN ArgParamList:l R_PAREN {:
            RESULT = new FunctionCallExprNode(idxleft, id, l);
        :} %prec FUNCTION_CALL
    | LENGTH:len L_PAREN ArgParamList:l R_PAREN {:
            RESULT = new LengthCallExprNode(lenxleft, l);
        :} %prec FUNCTION_CALL
    ;

ArgParamList ::=
    Expr:e COMMA ArgParamList:l {: l.addFirst(e); RESULT = l; :}
    | Expr:e {: RESULT = new LinkedList<>(List.of(e)); :}
    | {: RESULT = new LinkedList<>();:}
    ;

Access::=
    Access:a L_SQ_BRKT Expr:e R_SQ_BRKT {:
            RESULT = new ArrayAccessExprNode(axleft, a, e);
        :} 	%prec ARRAY_ACCESS
    | ID:id {: RESULT = new VariableAccessExprNode(idxleft, id); :}
    ;

LiteralExpr ::=
    INT_LITERAL:l {: RESULT = new LiteralIntExprNode(lxleft, l); :}
    | CHAR_LITERAL:l {: RESULT = new LiteralCharExprNode(lxleft, l); :}
    | STRING_LITERAL:l {: RESULT = new LiteralStringExprNode(lxleft, l); :}
    | BOOL_LITERAL:l {: RESULT = new LiteralBoolExprNode(lxleft, l); :}
    ;

UnaryExpr ::=
    NEG_BOOL Expr:e {: RESULT = new BoolNegExprNode(exleft, e); :}
    | MINUS Expr:e {: RESULT = new IntNegExprNode(exleft, e); :} %prec NEG_INT
    ;

IntegerExpr ::=
    Expr:e1 MULT Expr:e2 {: RESULT = new MultExprNode(e1xleft, e1, e2); :}
    | Expr:e1 HIGH_MULT Expr:e2 {: 
        RESULT = new HighMultExprNode(e1xleft, e1, e2); 
    :}
    | Expr:e1 DIVIDE Expr:e2 {: RESULT = new DivExprNode(e1xleft, e1, e2); :}
    | Expr:e1 REMAINDER Expr:e2 {: 
        RESULT = new RemExprNode(e1xleft, e1, e2); 
    :}
    | Expr:e1 PLUS Expr:e2 {: RESULT = new AddExprNode(e1xleft, e1, e2); :}
    | Expr:e1 MINUS Expr:e2 {: RESULT = new SubExprNode(e1xleft, e1, e2); :}
    ;

BoolExpr ::=
    Expr:e1 LT Expr:e2 {: RESULT = new LTExprNode(e1xleft, e1, e2); :}
    | Expr:e1 LTE Expr:e2 {: RESULT = new LTEExprNode(e1xleft, e1, e2); :}
    | Expr:e1 GT Expr:e2 {: RESULT = new GTExprNode(e1xleft, e1, e2); :}
    | Expr:e1 GTE Expr:e2 {: RESULT = new GTEExprNode(e1xleft, e1, e2); :}
    | Expr:e1 EQUALS Expr:e2 {: 
        RESULT = new EqualsExprNode(e1xleft, e1, e2); 
    :}
    | Expr:e1 NOT_EQUALS Expr:e2 {: 
        RESULT = new NotEqualsExprNode(e1xleft, e1, e2); 
    :}
    | Expr:e1 LOGICAL_AND Expr:e2 {: 
        RESULT = new AndExprNode(e1xleft, e1, e2); 
    :}
    | Expr:e1 LOGICAL_OR Expr:e2 {: 
        RESULT = new OrExprNode(e1xleft, e1, e2); 
    :}
    ;

ArrayExpr ::=
    L_BRACE ArrayElements:l R_BRACE {: 
        RESULT = new ArrayExprNode(lxleft, l); 
    :}
    ;

ArrayElements ::=
    Expr:e COMMA ArrayElements:l {:l.addFirst(e); RESULT = l;:}
    | Expr:e {:
            LinkedList<ExprNode> l = new LinkedList<>();
            l.add(e);
            RESULT = l;
         :}
    | {: RESULT = new LinkedList<>(); :}
    ;

